 100 PRINT "BASIC LIBRARY--'LUNAR'--18-OCT-73"
 110 PRINT
 120 PRINT" THIS PROGRAM IS DESIGNED TO SIMULATE THE LANDING OF"
 130 PRINT"   A LUNAR EXCURSION MODULE.  IF YOU KNOW HOW IT WORKS,"
 140 PRINT"    TYPE IN '1' ,IF NOT, TYPE IN '0'."
 150 INPUT A
 160 IF A=1 THEN320
 170 IF A=0 THEN 180
 180 PRINT
 190 PRINT"  YOU ARE TRAVELING IN THE LUNAR ATMOSPHERE AT AN "
 200 PRINT"   ALTITUDE OF 40,000 FEET, AND DESCENDING AT A RATE OF"
 210 PRINT"   8,000 FEET PER SECOND.  YOU HAVE ACCESS TO 20 "
 220 PRINT"    RETROROCKETS -- 1 TO 10 AND -1 TO -10."
 230 PRINT"    A POSITIVE NUMBER SLOWS DOWN YOUR DESCENT, AND A "
 240 PRINT"    NEGATIVE NUMBER INCREASES YOUR DESCENT. THE"
 250 PRINT"    NEGATIVE FIRINGS ARE PARTICULARLY USEFUL WHEN"
 260 PRINT"    YOU HAVE SLOWED DOWN  TOO MUCH, AND YOU"
 270 PRINT"    FIND YOUR HEIGHT INCREASING, INSTEAD OF DECREASING."
 280 PRINT"    YOU WILL LAND SAFELY IF YOUR FINAL VELOCITY IS"
 290 PRINT"     LESS THAN 20 FEET PER SECOND."
 300 PRINT"   AN ASTERISK (*) WILL GIVE YOUR RELATIVE POSITION"
 310 PRINT"    TO THE MOON AFTER EACH RETRO FIRE. YOU MAY NOW BEGIN..."
 320 PRINT
 330 PRINT"PLEASE TYPE IN YOUR FIRST NAME..."
 340 INPUT T$
 350 PRINT
 360 :             ##.##             #####             #####          ####
 370 PRINT"              TIME            HEIGHT            VELOCITY        FUEL"
 380 PRINT"              ----            ------            --------        ----"
 390 PRINT
 400 PRINT"MOON" TAB(45);"*"
 410 H=40000
 420 V=8000
 430 V1=8000
 440 J=RND(-1)
 450 LET J=INT(J*15)
 460 F=9000
 480 N=0.00
 490 B=40
 500 GO TO 610
 510 B=0
 520 FOR X=1 TO 100
 530 V=V-(K^3*1.5+K*15)+10-H/10000
 540 F=F-ABS(K*93)
 550 IF F<0 THEN 770
 560 H=H-(V+V1)/2
 570 B=H/1000+4
 580 IF H<0 THEN 800
 590 V1=V
 600 PRINT"MOON"; TAB(B);"*"
 610 PRINT USING 360,N,H,V,F
 620 PRINT"      FIRE";
 630 GO TO 650
 640 PRINT"  RETROROCKET FIRE MUST BE BETWEEN 10 AND -10!"
 650 INPUT K
 660 IF K=999 THEN 880
 670 IF ABS(K)>10 THEN 640
 680 PRINT
 690 N=N+1.00
 700 IF X=2 THEN 730
 710 NEXT X
 720 GO TO 510
 730 IF J=2 THEN 1140
 740 IF J=13 THEN 1180
 750 IF J=4 THEN 1340
 760 GO TO 510
 770 PRINT
 780 PRINT"  YOU HAVE JUST RUN OUT OF FUEL ";H;"FEET ABOVE THE SURFACE!!"
 790 GO TO 1560
 800 IF V<0 THEN 910
 810 IF V<20 THEN 950
 820 IFV<40 THEN 1570
 830 PRINT
 840 PRINT" YOU HAVE JUST CRASHED!! AT ";V;"FPS., GOOD-BYE, ";T$;"!!"
 850 GO TO 1560
 860 IF H<5 THEN 950
 870 GO TO 510
 880 PRINT"  YOU NOT ONLY LANDED, YOU WENT COMPLETELY THROUGH"
 890 PRINT"  THE LUNAR SURFACE !!!!"
 900 GO TO 980
 910 PRINT
 920 PRINT" CONGRATULATIONS !!! YOU HAVE JUST LANDED SAFELY AT 0.001 FEET"
 930 PRINT"  PER SECOND!!"
 940 GO TO 980
 950 PRINT
 960 PRINT" CONGRATULATIONS !!! YOU HAVE JUST LANDED SAFELY"
 970 PRINT"  AT ";V;" FEET PER SECOND!!"
 980 PRINT"  HERE IS YOUR PILOT\S LICENSE..."
 990 PRINT
1000 PRINT
1010 PRINT"**************************************************"
1020 PRINT"*                                                *"
1030 PRINT"*       ";T$;"                                    * "
1040 IF K=999 THEN 1070
1050 PRINT"*                  HAS SUCCESSFULLY LANDED       *"
1060 GO TO 1080
1070 PRINT"*           HAS SUCCESSFULLY LANDED(BY CHEATING) *"
1080 PRINT"*     A SIMULATED MODULE ON THE MOON, AND IS     *"
1090 PRINT"*     A CERTIFIED MOON BUGGY DRIVER.             *"
1100 PRINT"*                                                *"
1110 PRINT"*                              DATALINE          *"
1120 PRINT"**************************************************"
1130 GO TO 1560
1140 PRINT" SORRY,"T$", BUT A HOSTILE STARSHIP FROM SATURN HAS JUST"
1150 PRINT"   INVADED DATALINE'S COMPUTER AND DESTROYED YOUR"
1160 PRINT"   LUNAR VEHICLE !!  START OVER AGAIN..."
1170 GO TO 1560
1180 PRINT
1190 PRINT" ONE OF YOUR THREE FUEL TANKS JUST BURST INTO FLAME!!"
1200 PRINT"  TO AVOID AN EXPLOSION, TURN OFF ONE OF THE 3 TANKS."
1210 PRINT"  (TYPE IN 1, 2, OR 3 )...";
1220 INPUT R
1230 LET W=RND(-1)
1240 LET W=INT(W*3)+1
1250 IF W=R THEN 1300
1260 PRINT" SORRY ";T$;", BUT THE CORRECT FUEL TANK WAS NUMBER "W" , AND"
1270 PRINT"  YOU HAVE JUST BECOME INTERGALACTIC DUST!!"
1280 PRINT" START OVER AGAIN...."
1290 GO TO 1600
1300 PRINT" YOU HAVE PICKED THE CORRECT FUEL TANK, AND HAVE STOPPED"
1310 PRINT"  THE FIRE SUCCESSFULLY!! YOU MAY NOW CONTINUE SAFELY..."
1320 GO TO 710
1330 GO TO 1560
1340 PRINT
1350 PRINT"  A METEOR SHOWER IS JUST DEAD AHEAD!!!  IF YOU WANT TO"
1360 PRINT"     SAVE YOURSELF, YOU HAD BETTER VEER TO THE SIDE AND"
1370 PRINT"     FIRE SOME OF YOUR ANTI-METEOR ROCKETS!!  FROM 1 TO 15,"
1380 PRINT"     HOW MANY WILL YOU SHOOT ?"
1390 INPUT Z
1400 IF Z=9 THEN 1440
1410 IF Z=8 THEN 1440
1420 IF Z<8 THEN 1460
1430 IF Z>8 THEN 1510
1440 PRINT"  YOU MADE IT!!!   NOW YOU MAY CONTINUE..."
1450 GO TO 710
1460 PRINT
1470 PRINT" SORRY, "T$", BUT THAT WASN\T ENOUGH!!"
1480 PRINT"   YOU HAVE JUST BEEN DISINTEGRATED BY THE METEORS!!"
1490 PRINT"   START OVER AGAIN..."
1500 GO TO 1560
1510 PRINT
1520 PRINT"  SORRY,"T$", BUT YOU USED TOO MANY ROCKETS,"
1530 PRINT"    AND YOU ARE NOW HEADING SOMEWHERE TOWARDS MARS,"
1540 PRINT"    BECAUSE THE FIRING OF ALL THOSE ROCKETS THREW YOU"
1550 PRINT"    COMPLETELY OFF COURSE!! START OVER AGAIN..."
1560 STOP
1570 PRINT" YOU HAVE LANDED, BUT AT A VELOCITY OF ";V;" FEET PER SECOND,"
1580 PRINT"  WHICH HAS DAMAGED THE INSTRUMENTS IN YOUR SHIP ENOUGH TO"
1590 PRINT"  PREVENT YOUR RETURN TO THE EARTH!! BETTER LUCK NEXT TIME!"
1600 END
